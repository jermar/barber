rbytes  EQU 32
rows    EQU 256

pbytes  EQU 16

g_plane EQU 0a000h

black   EQU 8
red     EQU 9
blue    EQU 12
white   EQU 15

rgb     EQU 0cch

	ORG 1000h

	LXI B, 0	; BC used for pattern generation, C used as a line counter
	LXI D, 0ffffh	; DE used for pattern generation and to draw black border

	; generate the pattern
	; this results in shorter code than using a pre-generated 16-byte pattern
	LXI SP, pattern + pbytes
	PUSH B
	PUSH B
	PUSH D
	PUSH D
	PUSH B
	PUSH B
	PUSH D
	PUSH D

	LXI SP, g_plane + (rows * rbytes)

	MVI A, black
	OUT rgb

line:
	; right border
	PUSH D
	PUSH D
	PUSH D
	PUSH D

	LXI H, pattern
	MVI B, 8
pole:
	; pick color
	LXI D, color
	LDAX D
	RLC
	STAX D

	; convert CY to either red or blue
	MVI A, 8
	ACI 3
	ANI 0fdh

	OUT rgb

	; pick pattern
	MOV D, M
	INX H
	MOV E, M
	INX H

	PUSH D
	POP PSW

	; print inverted pattern in white 
	MVI A, white
	OUT rgb
	MOV A, E
	CMA
	MOV E, A
	MOV A, D
	CMA
	MOV D, A
	PUSH D

	DCR B
	JNZ pole

	; rotate the pattern
	MVI B, pbytes
	LXI H, pattern
	CMP A ; clear CY
rotate:
	; rotate the pattern
	MOV A, M
	RAL
	MOV M, A
	INX H
	DCR B
	JNZ rotate
	LXI H, pattern
	MOV A, M 
	ADC B 		; B is 0, only adding CY
	MOV M, A

	; rotate color every 16 lines
	MOV A, C
	ANI 0fh
	JNZ skip
	LXI H, color
	MOV A, M
	RRC
	MOV M, A
skip:
	
	; left border
	MVI A, black
	OUT rgb
	LXI D, 0ffffh
	PUSH D
	PUSH D
	PUSH D
	PUSH D

	DCR C
	JNZ line

spin:
	MVI B, 4
loop:
	DCR C
	JNZ loop
	DCR B
	JNZ loop 

	INR A
	OUT 0c0h
	JMP spin


; 0: red
; 1: blue
color:
	DB 0e1h

; 0: white
; 1: color
pattern:
; The following pattern is generated by the initialization code
;	DW 0ffffh, 0ffffh, 0, 0, 0ffffh, 0ffffh, 0, 0
	DS 16
